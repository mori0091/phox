use crate::syntax::ast::{RawTypeDecl, RawVariant, RawType, RawTraitHead};
use crate::syntax::ast::{RawTraitDecl, RawTraitMemberDecl};
use crate::syntax::ast::{RawImplDecl, RawImplMemberDecl};
use crate::syntax::ast::{Item, Stmt, Expr, Lit, Pat};

use crate::syntax::token::{Token, LexicalError};

grammar;

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token {
        // --- キーワード ---
        "type"  => Token::Type,
        "trait" => Token::Trait,
        "impl"  => Token::Impl,
        "let"   => Token::Let,
        "rec"   => Token::Rec,
        "if"    => Token::If,
        "else"  => Token::Else,
        "match" => Token::Match,
        "true"  => Token::True,
        "false" => Token::False,
        "()"    => Token::Unit,

        // --- 演算子・記号 ---
        "*"  =>  Token::Star,
        "/"  =>  Token::Slash,

        "+"  =>  Token::Plus,
        "-"  =>  Token::Minus,

        "==" =>  Token::Eq,
        "!=" =>  Token::Neq,
        "<"  =>  Token::Lt,
        "<=" =>  Token::Le,
        ">"  =>  Token::Gt,
        ">=" =>  Token::Ge,

        "&&" =>  Token::And,
        "||" =>  Token::Or,

        ">>" => Token::ComposeR,
        "<<" => Token::ComposeL,
        "|>" => Token::PipeR,
        "<|" => Token::PipeL,

        // --- 記号 ---
        "="  =>  Token::Assign,
        "->" =>  Token::Arrow,
        "=>" =>  Token::FatArrow,
        "@"  =>  Token::At,
        "\\" =>  Token::Backslash,
        "λ"  =>  Token::Lambda,
        "_"  =>  Token::Underscore,
        "."  =>  Token::Dot,
        ";"  =>  Token::Semi,
        ","  =>  Token::Comma,
        ":"  =>  Token::Colon,
        "`"  =>  Token::Backtick,
        "("  =>  Token::LParen,
        ")"  =>  Token::RParen,
        "{"  =>  Token::LBrace,
        "}"  =>  Token::RBrace,
        "["  =>  Token::LBracket,
        "]"  =>  Token::RBracket,
        "|"  =>  Token::VerticalBar,
        "!"  =>  Token::ExclamationMark,

        // --- 識別子・リテラル ---
        "identifier" => Token::Ident(<String>),
        "Identifier" => Token::ConIdent(<String>),
        "int"        => Token::Int(<i64>),

        // --- その他の演算子記号列 ---
        "infixOp" => Token::InfixOp(<String>),
    }
}

pub Program: Vec<Item> = {
    ItemList,
};

// ----------------------------------------------
pub ItemList: Vec<Item> = {
    <mut v:(<Item> ";")*> <tail:Expr> => {
        v.push(Item::Expr(tail));
        v
    },
    <mut v:(<Item> ";")+> => {
        v.push(Item::Expr(Expr::unit()));
        v
    },
};

pub Item: Item = {
    TraitDecl => Item::RawTraitDecl(<>),
    ImplDecl => Item::RawImplDecl(<>),
    TypeDecl => Item::RawTypeDecl(<>),
    Stmt => Item::Stmt(<>),
    Expr => Item::Expr(<>),
};

// ----------------------------------------------
TraitDecl: RawTraitDecl = {
    "trait" <name:ConIdent> <params:TypeParams> "{" <members:TraitMemberDecl+> "}"
        => RawTraitDecl { name: name, params: params, members: members, },
};

TraitMemberDecl: RawTraitMemberDecl = {
    <name:Ident> ":" <ty:Type> ";"
        => RawTraitMemberDecl { name: name, ty: Box::new(ty), },
};

// ----------------------------------------------
ImplDecl: RawImplDecl = {
    "impl" <name:ConIdent> <params:TypeAtom+> "{" <members:ImplMemberDecl+> "}"
        => RawImplDecl { name: name, params: params, members: members, },
};

ImplMemberDecl: RawImplMemberDecl = {
    <name:Ident> "=" <expr:Expr> ";"
        => RawImplMemberDecl { name: name, expr: Box::new(expr) },
};

// ----------------------------------------------
TypeDecl: RawTypeDecl = {
    "type" <name:ConIdent> <params:TypeParams> "=" <variants:VariantList>
        => RawTypeDecl::SumType {
            name: name.clone(),
            params: params,
            variants: variants,
        },
    "type" <name:ConIdent> <params:TypeParams> "=" <tuple:TypeTuple>
        => RawTypeDecl::SumType {
            name: name.clone(),
            params: params,
            variants: vec![RawVariant::Tuple(name.clone(), vec![tuple])],
        },
    "type" <name:ConIdent> <params:TypeParams> "=" <record:TypeRecord>
        => RawTypeDecl::SumType {
            name: name.clone(),
            params: params,
            variants: vec![RawVariant::Tuple(name.clone(), vec![record])],
        },
};

TypeParams: Vec<String> = {
    <vars:Ident*> => vars
};

VariantList: Vec<RawVariant> = {
    <mut vs:(<Variant> "|")*> <v:Variant> => {
        vs.push(v);
        vs
    }
};

Variant: RawVariant = {
    // VariantRecord,
    VariantSimple,
};

VariantSimple: RawVariant = {
    <name:ConIdent> <ts:TypeAtom*> => {
        if ts.is_empty() {
            RawVariant::Unit(name)
        }
        else {
            RawVariant::Tuple(name, ts)
        }
    }
};

Type: RawType = {
    TypeFun,
};

TypeFun: RawType = {
    <l:TypeApp> "->" <r:TypeFun> => RawType::Fun(Box::new(l), Box::new(r)),
    TypeApp,
};

TypeApp: RawType = {
    <f:TypeApp> <x:TypeAtom> => RawType::App(Box::new(f), Box::new(x)),
    TypeAtom,
};

TypeAtom: RawType = {
    TypeVar,
    TypeCon,
    TypeParen,
    TypeTuple,
    TypeRecord,
};

TypeVar: RawType = {
    Ident => RawType::VarName(<>),
};

TypeCon: RawType = {
    ConIdent => RawType::ConName(<>),
    <"()"> => RawType::ConName("()".to_string()),
};

TypeParen: RawType = {
    "(" <t:Type> ")" => t,
};

TypeTuple: RawType = {
    "(" <ts:SepByComma<Type>> ")" => RawType::Tuple(ts),
};

TypeRecord: RawType = {
    "@" "{" <fields:SepByComma0<TypeRecordField>> "}"
        => RawType::Record(fields)
};

TypeRecordField: (String, RawType) = {
    <name:Ident> ":" <value:Type> => (name, value)
};

// ----------------------------------------------
lambda: Token = {
    "\\",
    "λ",
};

// ----------------------------------------------
Stmt: Stmt = {
    StmtLet,
    StmtLetRec,
};

StmtLet: Stmt = {
    "let" <p:Pat> "=" <e:Expr> => Stmt::Let(p, Box::new(e)),
};

StmtLetRec: Stmt = {
    "let" "rec" <p:Pat> "=" <e:Expr> => Stmt::LetRec(p, Box::new(e)),
};

// ----------------------------------------------
pub Expr: Expr = {
    Expr0,
};

Expr0: Expr = {
    ExprAbs,
    ExprIf,
    ExprMatch,
    Expr1,
};

ExprAbs: Expr = {
    lambda <p:Pat> "." <body:Expr0> => Expr::abs(p, body),
};

ExprIf: Expr = {
    "if" "(" <cond:Expr0> ")" <e1:Expr0> "else" <e2:Expr0> => Expr::if_(cond, e1, e2),
};

ExprMatch: Expr = {
    "match" "(" <scrut:Expr0> ")" "{" <arms:SepByComma0<MatchArm>> "}"
        => Expr::match_(scrut, arms)
};

MatchArm: (Pat, Expr) = {
    <p:Pat> "=>" <e:Expr> => (p, e)
};

Expr1: Expr = {
    ExprBool,
    Expr2,
};

// 論理演算
ExprBool: Expr = {
    <l:Expr1> "&&" <r:Expr2> => Expr::app(Expr::app(Expr::unresolved_var("&&"), l), r),
    <l:Expr1> "||" <r:Expr2> => Expr::app(Expr::app(Expr::unresolved_var("||"), l), r),
};

Expr2: Expr = {
    ExprCmp,
    Expr3,
};

// 比較演算子 (infix)
ExprCmp: Expr = {
    <l:Expr3> "==" <r:Expr3> => Expr::app(Expr::app(Expr::unresolved_var("=="), l), r),
    <l:Expr3> "!=" <r:Expr3> => Expr::app(Expr::app(Expr::unresolved_var("!="), l), r),
    <l:Expr3> "<"  <r:Expr3> => Expr::app(Expr::app(Expr::unresolved_var("<") , l), r),
    <l:Expr3> "<=" <r:Expr3> => Expr::app(Expr::app(Expr::unresolved_var("<="), l), r),
    <l:Expr3> ">"  <r:Expr3> => Expr::app(Expr::app(Expr::unresolved_var(">") , l), r),
    <l:Expr3> ">=" <r:Expr3> => Expr::app(Expr::app(Expr::unresolved_var(">="), l), r),
};

Expr3: Expr = {
    ExprAdd,
    Expr4,
};

// 加減算 (infixl)
ExprAdd: Expr = {
    <l:Expr3> "+" <r:Expr4> => Expr::app(Expr::app(Expr::unresolved_var("+"), l), r),
    <l:Expr3> "-" <r:Expr4> => Expr::app(Expr::app(Expr::unresolved_var("-"), l), r),
};

Expr4: Expr = {
    ExprMul,
    Expr5,
};


// 乗除算 (infixl)
ExprMul: Expr = {
    <l:Expr4> "*" <r:Expr5> => Expr::app(Expr::app(Expr::unresolved_var("*"), l), r),
    <l:Expr4> "/" <r:Expr5> => Expr::app(Expr::app(Expr::unresolved_var("/"), l), r),
};

Expr5: Expr = {
    ExprInfixApp,
    Expr6,
};

// 中置構文
ExprInfixApp: Expr = {
    <l:Expr5> "`" <f:Atom> "`" <r:Expr6> => {
        Expr::app(Expr::app(f, l), r)
    },
};

Expr6: Expr = {
    ExprInfixOp,
    Expr7,
};

ExprInfixOp: Expr = {
    <l:Expr6> <op:"infixOp"> <r:Expr7> => {
        Expr::app(Expr::app(Expr::unresolved_var(op), l), r)
    },
};

Expr7: Expr = {
    ExprPipeL,
    Expr8,
};

ExprPipeL: Expr = {
    <f:Expr8> "<|" <x:Expr7> => Expr::app(Expr::app(Expr::unresolved_var("<|"), f), x),
};

Expr8: Expr = {
    ExprPipeR,
    Expr9,
};

ExprPipeR: Expr = {
    <x:Expr8> "|>" <f:Expr9> => Expr::app(Expr::app(Expr::unresolved_var("|>"), x), f),
};

Expr9: Expr = {
    ExprCompose,
    Expr10,
};

ExprCompose: Expr = {
    <f:Expr9> ">>" <g:Expr10> => Expr::app(Expr::app(Expr::unresolved_var(">>"), f), g),
    <f:Expr9> "<<" <g:Expr10> => Expr::app(Expr::app(Expr::unresolved_var("<<"), f), g),
};

Expr10: Expr = {
    ExprUnary,
    Expr11,
};

// 単項演算子 (prefix)
ExprUnary: Expr = {
    "-" <r:Expr10> => Expr::app(Expr::unresolved_var("negate"), r),
    "!" <r:Expr10> => Expr::app(Expr::unresolved_var("not"), r),
};

Expr11: Expr = {
    ExprApp,
    Atom,
};

// 関数適用（左結合）
ExprApp: Expr = {
    <f:Expr11> <x:Atom> => Expr::app(f, x),
};

// 原子的な式
Atom: Expr = {
    Lit => Expr::lit(<>),
    Ident => Expr::unresolved_var(<>),
    ConIdent => Expr::unresolved_var(<>),
    ExprParen,
    ExprTuple,
    ExprRecord,
    ExprTraitRecord,
    ExprBlock,
    ExprFieldAccess,
    ExprTupleAccess,
};

ExprParen: Expr = {
    "(" <e:Expr> ")" => e,
};

ExprTuple: Expr = {
    "(" <es:SepByComma<Expr>> ")" => Expr::tuple(es),
};

ExprRecord: Expr = {
    "@" "{" <fields:SepByComma0<ExprRecordField>> "}"
        => Expr::record(fields)
};

ExprTraitRecord: Expr = {
    "@" "{" <raw:TraitHead> "}" => {
        Expr::raw_trait_record(raw)
    },
};

TraitHead: RawTraitHead = {
    <name:ConIdent> <params:TypeAtom+> => {
        RawTraitHead { name, params }
    },
};

ExprRecordField: (String, Expr) = {
    <name:Ident> ":" <value:Expr> => (name, value)
};

ExprFieldAccess: Expr = {
    <base:Atom> "." <field:Ident> => Expr::field_access(base, field),
};

ExprTupleAccess: Expr = {
    <base:Atom> "." <index:Int> => Expr::tuple_access(base, index as usize),
};

ExprBlock: Expr = {
    "{" "}" => {
        Expr::block(vec![Item::Expr(Expr::unit())])
    },
    "{" <items:ItemList> "}" => {
        Expr::block(items)
    },
};

// === pattern ===
Pat: Pat = {
    Pat0,
};

Pat0: Pat = {
    PatCon,
    PatAtom,
};

PatCon: Pat = {
    <name:ConIdent> <args:PatAtom*> => Pat::unresolved_con(name, args),
};

PatAtom: Pat = {
    "_" => Pat::Wildcard,
    Lit => Pat::Lit(<>),
    Ident => Pat::unresolved_var(<>),
    PatParen,
    PatTuple,
    PatRecord,
};

PatParen: Pat = {
    "(" <p:Pat> ")" => p,
};

PatTuple: Pat = {
    "(" <ps:SepByComma<Pat>> ")" => Pat::Tuple(ps),
};

PatRecord: Pat = {
    "@" "{" <fields:SepByComma0<PatRecordField>> "}"
        => Pat::Record(fields)
};

PatRecordField: (String, Pat) = {
    <name:Ident> ":" <pat:Pat> => (name, pat),
    <name:Ident> => (name.clone(), Pat::unresolved_var(name)) // 省略形
};

// === literal ===
Lit: Lit = {
    LitUnit,
    LitInt,
    LitBool,
};

LitUnit: Lit = {
    "()" => Lit::Unit,
};

LitBool: Lit = {
    "true" => Lit::Bool(true),
    "false" => Lit::Bool(false),
};

LitInt: Lit = {
    <Int> => Lit::Int(<>),
};

Int: i64 = {
    <a:"int"> => a,
};

// === identifier ===
Ident: String = {
    <"identifier">,
    "(" <"infixOp"> ")",
    "(" "==" ")" => "==".to_string(),
    "(" "!=" ")" => "!=".to_string(),
    "(" "<" ")"  => "<".to_string(),
    "(" "<=" ")" => "<=".to_string(),
    "(" ">" ")"  => ">".to_string(),
    "(" ">=" ")" => ">=".to_string(),
    "(" "+" ")"  => "+".to_string(),
    "(" "-" ")"  => "-".to_string(),
    "(" "*" ")"  => "*".to_string(),
    "(" "/" ")"  => "/".to_string(),
    "(" "!" ")"  => "not".to_string(),
    "(" "&&" ")" => "&&".to_string(),
    "(" "||" ")" => "||".to_string(),
    "(" "|>" ")" => "|>".to_string(),
    "(" "<|" ")" => "<|".to_string(),
    "(" ">>" ")" => ">>".to_string(),
    "(" "<<" ")" => "<<".to_string(),
};

ConIdent: String = {
    <"Identifier">,
};

// === comma-separated list ===
// A comma-separated list of T elements. (1 or more)
// Except when there is only one element, the trailing comma is optional.
SepByComma<T>: Vec<T> = {
    <mut v:(<T> ",")+> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

// A comma-separated list of T elements. (0 or more)
// The trailing comma is optional.
SepByComma0<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
